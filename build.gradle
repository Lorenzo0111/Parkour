plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id("xyz.jpenilla.run-paper") version "1.0.6"
}

group = 'me.lorenzo0111'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://jitpack.io/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    implementation 'com.github.glyart:mystral:1.4.4-SNAPSHOT'
    implementation 'dev.triumphteam:triumph-gui:3.1.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set("")
    relocate("com.glyart.mystral", "me.lorenzo0111.parkour.data.sql.mysql")
    relocate("dev.triumphteam.gui", "me.lorenzo0111.parkour.gui.handler")
}

runServer {
    minecraftVersion("1.18.2")
}

tasks.build.dependsOn tasks.shadowJar